plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android-extensions'
}

android {
    compileSdk 32

    Properties properties = new Properties()
    if (rootProject.file("local.properties").exists()) {
        properties.load(rootProject.file("local.properties").newDataInputStream())
    }

    defaultConfig {
        applicationId "com.mzhang.ably_demo"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "ABLY_API_KEY", "\"${properties.getProperty('ABLY_API_KEY')}\""
        buildConfigField "String", "MAPBOX_ACCESS_TOKEN", "\"${properties.getProperty('MAPBOX_ACCESS_TOKEN')}\""
        resValue "string", "mapbox_access_token", properties.getProperty('MAPBOX_ACCESS_TOKEN')

        resValue 'string', 'google_maps_api_key', "${properties.getProperty('GOOGLE_MAPS_API_KEY')}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude : ['*mock*.jar'])

    def multidex_version = "2.0.1"
    implementation("androidx.multidex:multidex:$multidex_version")

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "io.ably:ably-android:1.2.11"
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.amplifyframework:aws-storage-s3:1.4.1'
    implementation 'com.amplifyframework:aws-auth-cognito:1.4.1'
    implementation 'pub.devrel:easypermissions:3.0.0'
    // This version needs to be compatible with the "com.mapbox.navigation:core" dependency version from publishing-sdk.
    implementation 'com.mapbox.maps:android:10.3.0'

    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'androidx.fragment:fragment:1.4.1'


    implementation 'com.jakewharton.timber:timber:5.0.1'

    // =======
    // The MapBox Navigation SDK for Android.
    // We're not using the pre-built UI components, so just need the core dependency.
    // https://docs.mapbox.com/android/navigation/overview/
    implementation('com.mapbox.navigation:core:2.3.0') {
        // We provide a custom trip notification so we exclude the default one.
        // https://docs.mapbox.com/android/navigation/guides/modularization/#tripnotification
        exclude group: "com.mapbox.navigation", module: "notification"
    }
    // The special asset tracking profile for the Mapbox Navigation.
    implementation("com.mapbox.navigator:mapbox-navigation-native:ak-2.3-tracking-profile-4-SNAPSHOT") {
        force = true
    }

    // Dependencies needed for replacing Mapbox modules.
    // https://docs.mapbox.com/android/navigation/guides/modularization/#replacing-a-module
    compileOnly("com.mapbox.base:annotations:0.5.0")
//    kapt("com.mapbox.base:annotations-processor:0.4.0")

    // Only the Core SDK portion of the MapBox Maps SDK for Android.
    // https://docs.mapbox.com/android/maps/overview/
    // https://docs.mapbox.com/android/core/overview/
    implementation 'com.mapbox.mapboxsdk:mapbox-android-core:5.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}